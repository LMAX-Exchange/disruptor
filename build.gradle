/*
 * Copyright 2011 LMAX Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'   // Only used so the Eclipse STS Gradle plugin can see the 'perf' source set dependencies. :-(
apply plugin: 'checkstyle'
apply plugin: 'idea'

defaultTasks 'build'

group = 'com.lmax'
version = new Version(major: 3, minor: 4, revision: 3)

ext {
    fullName = 'Disruptor Framework'
    fullDescription = 'Disruptor - Concurrent Programming Framework'
    teamName = 'LMAX Disruptor Development Team'
    siteUrl = 'http://lmax-exchange.github.com/disruptor'
    sourceUrl = 'git@github.com:LMAX-Exchange/disruptor.git'
    moduleName = 'com.lmax.disruptor'

    javaCompilerExecutable = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'

    if (!project.hasProperty('sonatypeUrl')) sonatypeUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    if (!project.hasProperty('sonatypeUsername')) sonatypeUsername = ''
    if (!project.hasProperty('sonatypePassword')) sonatypePassword = ''
}

sourceSets {
    perf.java.srcDir file('src/perftest/java')
}

eclipse.classpath.plusConfigurations += [ sourceSets.perf.compileClasspath ]

repositories {
    mavenCentral()
}

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:8.12'
    testCompile 'junit:junit:4.12'
    perfCompile 'org.hdrhistogram:HdrHistogram:2.1.10'
}

idea.module {
    testSourceDirs += sourceSets.perf.allSource.srcDirs
    scopes.TEST.plus += [ configurations.perfCompile ]
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


compileJava {
    // Suppress warnings about using Unsafe and sun.misc
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.debug = true
    options.forkOptions.executable = javaCompilerExecutable
    options.warnings = false
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

compilePerfJava {
    classpath += sourceSets.main.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath
}

javadoc {
    title = 'Disruptor'

    options.addStringOption('XDignore.symbol.file', '-quiet')
    options.author = true
    options.bottom = "<i>Copyright &#169; 2011 - ${Calendar.instance[Calendar.YEAR]} LMAX Ltd. All Rights Reserved.</i>"
    options.use = true
    options.version = true
    options.showFromPublic()
}

jar {
    manifest.attributes('Built-By': System.properties['user.name'],
                        'Bundle-Name': fullName,
                        'Bundle-Vendor': teamName,
                        'Bundle-Description': fullDescription,
                        'Bundle-DocURL': siteUrl,
                        'Automatic-Module-Name': moduleName)
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def projectPom = {
    name = fullName
    description = fullDescription
    url = siteUrl

    scm {
        url = "scm:$sourceUrl"
        connection = "scm:$sourceUrl"
    }

    licenses {
        license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
        }
    }

    developers {
        developer {
            id = 'team'
            name = teamName
            email = 'lmax-disruptor@googlegroups.com'
        }
    }
}

install {
    repositories.mavenInstaller.pom.project(projectPom)
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { deployment -> signing.signPom(deployment) }

        repository(url: sonatypeUrl) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project(projectPom)
    }
}

build.dependsOn perfClasses

task perfJar(type: Jar) {
    baseName = project.name + '-perf'
    from { configurations.perfCompile.collect { it.isDirectory() ? it : zipTree(it) } }
    from sourceSets.perf.output
    from sourceSets.test.output
    with jar
}

wrapper {
    gradleVersion = '5.1'
}

class Version {
    int major, minor = 0, revision = 0
    boolean snapshot
    String stage

    String toString() {
        "$major.$minor.$revision${stage ? '.' + stage : ''}${snapshot ? '-SNAPSHOT' : ''}"
    }
}
